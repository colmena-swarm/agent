plugins {
    id 'application' //Application.main method to be run as a gradle task
    id 'java'
    id 'com.google.protobuf' version '0.9.4'
}

group = 'es.bsc.colmena.library'
version = '0.0.1'

java {
    sourceCompatibility = "17"
}

repositories {
    mavenCentral()
}

def grpcVersion = '1.58.0'
def protobufVersion = '3.24.0'
def protocVersion = protobufVersion
dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

    implementation 'com.google.guava:guava:32.1.2-jre'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'org.slf4j:slf4j-simple:2.0.7'

    //prometheus
    implementation 'io.prometheus:prometheus-metrics-core:1.0.0'
    implementation 'io.prometheus:prometheus-metrics-instrumentation-jvm:1.0.0'
    implementation 'io.prometheus:prometheus-metrics-exporter-httpserver:1.0.0'

    //grpc
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-services:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "com.google.protobuf:protobuf-java-util:3.24.4"
    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    //docker
    implementation "com.github.docker-java:docker-java-core:3.3.2"
    implementation "com.github.docker-java:docker-java:3.3.2"
    implementation "com.github.docker-java:docker-java-transport-httpclient5:3.3.2"

    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.3'
    testImplementation 'org.awaitility:awaitility:4.2.0'
    testImplementation 'org.mockito:mockito-core:3.+'
    testImplementation 'org.hamcrest:hamcrest:2.2'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
